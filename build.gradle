buildscript {
    ext.kotlin_version = '1.2.51'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:+"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4"
    }
}

plugins {
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.8.1"
}
apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "org.junit.platform.gradle.plugin"

// MainClassName + Kt
mainClassName = 'sds.MainKt'

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled  = true
    }
}

junitPlatform {
    // reportsDir file("build/test-results")
    filters {
        engines {
            include 'spek'
        }
        includeClassNamePattern ".*Spec"
    }
    platformVersion "1.0.0-M4"
}

repositories {
    jcenter() // for kluent
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:+"
    testCompile 'org.jetbrains.spek:spek-api:1.1.1'
    testCompile 'org.amshove.kluent:kluent:1.21'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.1'
}

sourceSets {
    main {
        kotlin.srcDir 'src/main/kotlin'
    }
    test {
        kotlin.srcDir 'src/test/kotlin'
        resources.srcDirs 'src/test/resources'
    }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from configurations.compile.collect { it.isDirectory() ? it :  zipTree(it) }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}